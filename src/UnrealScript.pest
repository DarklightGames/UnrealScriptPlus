comment_single_line = _{ "//" ~ (!"\n" ~ ANY)* }
comment_multi_line = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

COMMENT = _{ comment_single_line | comment_multi_line }
WS = _{ " " | "\n" | "\t" }

WHITESPACE = _{ "\r" } // This symbol is truly useless, so just ignore it globally

compiler_directive_inner = { (!"\n" ~ ANY)* }
compiler_directive = { ^"#" ~ compiler_directive_inner ~ (("\n") | eoi)}

alpha = @{ 'a'..'z' | 'A'..'Z' }
digit = @{ '0'..'9' }
unqualified_identifier = @{ (alpha | "_") ~ (alpha | digit | "_")* }
unqualified_identifier_list = { unqualified_identifier ~ (WS* ~ "," ~ WS* ~  unqualified_identifier)* }
qualified_identifier = @{ unqualified_identifier ~ "." ~ unqualified_identifier }
identifier = @{ qualified_identifier | unqualified_identifier }

// literals
hex_digits = { ASCII_HEX_DIGIT+ }
decimal_digits = { digit+ }
integer_literal_decimal = @{ decimal_digits }
integer_literal_hexadecimal = { ^"0x" ~ hex_digits }
positive_or_negative = { "-" | "+" }
integer_literal = { positive_or_negative? ~ (integer_literal_hexadecimal | integer_literal_decimal) }

float_literal_decimal = @{ digit+ ~ "." ~ digit* }
float_literal_scientific = @{ digit+ ~ ("." ~ digit*)? ~ "e" ~ digit+ }

// has to have *either* the decimal or the E in order to be a float literal!
float_literal = @{ ("-" | "+")? ~ (float_literal_scientific | float_literal_decimal) }
numeric_literal = _{ float_literal | integer_literal }

string_character = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
string_literal_inner = @{ string_character* }
string_literal = { "\"" ~ string_literal_inner ~ "\"" }

name_character = @{ ASCII_ALPHANUMERIC | "_" }
name_inner = @{ name_character* }
name_literal = { "'" ~ name_inner ~ "'"}
single_quoted_string_character = @{ ASCII_ALPHANUMERIC | "." | " " | "_" }
single_quoted_string_inner = @{ single_quoted_string_character* }
single_quoted_string = { "'" ~ single_quoted_string_inner  ~ "'"}
object_literal = { unqualified_identifier ~ single_quoted_string }
vector_literal = { ^"vect" ~ WS* ~ "(" ~ WS* ~ numeric_literal ~ WS* ~ "," ~ WS* ~ numeric_literal ~ WS* ~ "," ~ WS* ~ numeric_literal ~ WS* ~ ")" }
rotator_literal = { ^"rot" ~ WS* ~ "(" ~ WS* ~ numeric_literal ~ WS* ~ "," ~ WS* ~ numeric_literal ~ WS* ~ "," ~ WS* ~ numeric_literal ~ WS* ~ ")" }
literal = {
    vector_literal |
    rotator_literal |
    numeric_literal |
    string_literal |
    name_literal |
    object_literal
}

class_modifier_type = @{
    ^"abstract" |
    ^"cacheexempt" |
    ^"config" |
    ^"dependson" |
    ^"guid" |
    ^"instanced" |
    ^"parseconfig" |
    ^"perobjectconfig" |
    ^"safereplace" |
    ^"transient" |
    ^"collapsecategories" |
    ^"dontcollapsecategories" |
    ^"editinlinenew" |
    ^"noteditinlinenew" |
    ^"hidecategories"|
    ^"showcategories"|
    ^"hidedropdown" |
    ^"placeable" |
    ^"notplaceable" |
    ^"exportstructs" |
    ^"intrinsic" |
    ^"nativereplication" |
    ^"native" |
    ^"noexport"
}
class_modifier = { class_modifier_type ~ (WS* ~ "(" ~ WS* ~ expression_list ~ WS* ~ ")")? }
class_declaration = { ^"class" ~ WS+ ~ unqualified_identifier ~ (WS+ ~ extends)? ~ (WS+ ~ class_modifier)* ~ WS* ~ ";" }

var_modifier = {
    ^"const" |
    ^"deprecated" |
    ^"private" |
    ^"protected" |
    ^"public" |
    ^"automated" |
    ^"config" |
    ^"globalconfig" |
    ^"input" |
    ^"localized" |
    ^"transient" |
    ^"travel" |
    ^"edfindable" |
    ^"editconst" |
    ^"editconstarray" |
    ^"editinline" |
    ^"editinlineuse" |
    ^"cache" |
    ^"editinlinenotify" |
    ^"export" |
    ^"native" |
    ^"noexport"
}
var_size = { "[" ~ WS* ~ (integer_literal | unqualified_identifier) ~ WS* ~ "]" }
var_name = { unqualified_identifier ~ WS* ~ var_size? }
var_category = { "(" ~ WS* ~ unqualified_identifier? ~ WS* ~ ")" }
var_modifiers = _{ var_modifier ~ (WS+ ~ var_modifier)* }
var_declaration = { ^"var" ~ (WS* ~ var_category)? ~ (WS+ ~ var_modifiers)? ~ WS+ ~ type_ ~ WS+ ~ var_names ~ ";" }
struct_var_editable = { "(" ~ WS* ~ ")" }
struct_var_declaration = { ^"var" ~ (WS* ~ struct_var_editable)? ~ (WS+ ~ var_modifier)* ~ WS+ ~ type_ ~ WS+ ~ var_names ~ WS* ~ ";" }
struct_var_declarations = _{ struct_var_declaration ~ (WS+ ~ struct_var_declaration)* }
array_type = { ^"array" ~ WS* ~ "<" ~ WS* ~ type_ ~ WS* ~ ">" }
class_type = { ^"class" ~ WS* ~ "<" ~ WS* ~ identifier ~ WS* ~ ">" }
type_ = { struct_declaration | enum_declaration | array_type | class_type | identifier }

const_declaration = { ^"const" ~ WS+ ~ unqualified_identifier ~ WS* ~ "=" ~ WS* ~ literal ~ WS* ~ ";" }
enum_declaration = { ^"enum" ~ WS+ ~ unqualified_identifier ~ WS* ~ "{" ~ WS* ~ unqualified_identifier_list ~ ","? ~ WS* ~ "}"}
enum_statement = { enum_declaration ~ ";" }

struct_modifier = {
    ^"long" |
    ^"transient" |
    ^"export" |
    ^"init" |
    ^"native"
}
struct_modifiers = { struct_modifier* }
extends = { ^"extends" ~ WS+ ~ identifier }
struct_declaration = { ^"struct" ~ (WS+ ~ struct_modifier)* ~ WS+ ~ unqualified_identifier ~ WS+ ~ extends? ~ WS* ~ "{" ~ WS* ~ struct_var_declarations? ~ WS* ~ "}" }

function_modifier_type = {
    ^"native" |
    ^"private" |
    ^"protected" |
    ^"public" |
    ^"static" |
    ^"final" |
    ^"exec" |
    ^"simulated" |
    ^"singular" |
    ^"intrinsic" |
    ^"iterator" |
    ^"latent"
}
function_modifier = { function_modifier_type ~ (WS* ~ "(" ~ WS* ~ integer_literal ~ WS* ~ ")")? ~ WS+ }
function_argument_modifier = {
    ^"coerce" |
    ^"optional" |
    ^"out" |
    ^"skip"
}
function_argument_modifiers = _{ function_argument_modifier ~ (WS+ ~ function_argument_modifier)* }
function_argument = { (function_argument_modifiers ~ WS+)? ~ type_ ~ WS+ ~ var_name}
function_arguments = _{ function_argument ~ (WS* ~ "," ~ WS* ~ function_argument)* }
function_type = { ^"function" | ^"event" | ^"delegate" }
function_type_outer = _{ function_type ~ WS+ }
function_declaration = { (function_modifier+)? ~ function_type_outer+ ~ (function_modifier+)? ~ ((type_ ~ WS+ ~ unqualified_identifier) | unqualified_identifier) ~ WS* ~ "(" ~ WS* ~ function_arguments? ~ WS* ~ ")" ~ WS* ~ body_or_semicolon }

operator = { ^"operator" ~  WS* ~ "(" ~ WS* ~ integer_literal ~ WS* ~ ")" }
preoperator = { ^"preoperator" }
postoperator = { ^"postoperator" }
operator_type = { operator | preoperator | postoperator }

// TODO: fix the VERBS
any_verb = { symbolic_verb | unqualified_identifier }
operator_declaration = { (function_modifier* ~ WS+)? ~ operator_type ~ WS+ ~ type_ ~ WS+ ~ any_verb ~ WS* ~ "(" ~ WS* ~ function_arguments? ~ WS* ~ ")" ~ WS* ~ body_or_semicolon }

body_or_semicolon = _{ (";" | "{" ~ WS* ~ code_body ~ WS* ~ "}") }

reliability = { ^"reliable" | ^"unreliable" }
replication_statement = { reliability ~ WS+ ~ ^"if" ~ WS+ ~ parenthetical_expression ~ WS* ~ (unqualified_identifier ~ (WS* ~ "," ~ WS* ~ unqualified_identifier)*) ~ WS* ~ ";" }
replication_statements = _{ replication_statement ~ (WS+ ~ replication_statement) }
replication_block = { ^"replication" ~ WS* ~ "{" ~ WS* ~ replication_statements? ~ WS* ~ "}" }

type_declaration_statement = _{ (enum_declaration | struct_declaration) ~ ";" }
class_statement = _{ compiler_directive | const_declaration | var_declaration  | type_declaration_statement | function_declaration | operator_declaration | replication_block | state_declaration }
class_statements = _{ class_statement ~ (WS* ~ class_statement)* }
eoi = _{ EOI }
program = { SOI ~ class_declaration ~ WS* ~ class_statements? ~ eoi }

var_names = _{ var_name ~ (WS* ~ "," ~ WS* ~ var_name)* }
local_declaration = { ^"local" ~ WS+ ~ type_ ~ WS+ ~ var_names ~ WS* ~ ";" }

// LOGIC
// build a straight LIST of what we see in front of us, we will shuffle the AST around in a separate pass!
array_access = { "[" ~ WS* ~ expression ~ WS* ~ "]" }
member_access = { "." ~ WS* ~ unqualified_identifier }
default_access = { ^"default" ~ WS* ~ "." ~ WS* ~ unqualified_identifier }
default_call = { ^"default" ~ WS* ~ "." ~ WS* ~ unqualified_identifier ~ WS* ~ "(" ~ WS* ~ expression_list ~ WS* ~ ")" }
static_acesss = { ^"static" ~ WS* ~ "." ~ WS* ~ unqualified_identifier }
global_call = { ^"global" ~ WS* ~ "." ~ WS* ~ unqualified_identifier ~ WS* ~ "(" ~ WS* ~ expression_list ~ WS* ~ ")" }
super_call = { ^"super" ~ WS* ~ ("(" ~ WS* ~ unqualified_identifier ~ WS* ~ ")")? ~ WS* ~ "." ~ WS* ~ unqualified_identifier ~ WS* ~ "(" ~ WS* ~ expression_list ~ WS* ~ ")" }
atom = _{ call | array_access | ("." ~ default_access) | ("." ~ static_acesss) | member_access | dyadic_expression }

dyadic_expression = _{ dyadic_verb ~ WS* ~ expression }

// TODO: chaining unary operators don't work very well (eg. ~--A) [valid uc, invalid ucp]
monadic_verb = { monadic_prefix_verb | monadic_post_verb }

symbolic_verb = {
    "++" | "--" | "**" | "+=" | "-=" | "*=" | "/=" | "&&" | "^^" |
    "||" | "-=" | "<<" | ">>>" | ">>" | "<=" | ">=" | "==" | "~=" | "<" | ">" | "!=" |
    "$=" | "@=" | "$" | "@" | "+" | "-" | "=" | "&" | "|" | "^" | "%" | "*" | "/" | "!" | "~"
}

monadic_prefix_verb = { "++" | "--" | "~" | "-" | "!" }
monadic_post_verb = { "++" | "--" }
monadic_pre_expression = _{ monadic_prefix_verb ~ WS* ~ target }
monadic_post_expression = _{ target ~ WS* ~ monadic_post_verb }
monadic_expression = _{ monadic_pre_expression | monadic_post_expression }

primary = _{ cast | literal | parenthetical_expression | default_access | static_acesss | global_call | unqualified_identifier }   // primaries are the START of a TARGET
target = _{ new | (primary ~ (WS* ~ atom)*) }
parenthetical_expression = { "(" ~ WS* ~ expression ~ WS* ~ ")" }
expression = _{ monadic_expression | target | unqualified_identifier }   // TODO: monadic expression should be a target???

// new is a kind of "terminal", nothing can come after it in an expression
new = { ^"new" ~ (WS* ~ "(" ~ expression_list ~ ")")? ~ WS+ ~ expression } /// fuck this syntax

call = { "(" ~ WS* ~ expression_list ~ WS* ~ ")" }
empty_expression = { "," }
expression_list = _{ (expression ~ WS* ~ "," ~ WS* | (empty_expression ~ WS*))* ~ expression? }
constant_list = _{ (constant ~ "," | empty_expression)* ~ constant? }
cast = { class_type ~ WS* ~ parenthetical_expression }
// TODO: certain casts (int, string) don't follow the cast grammar

code_scope = { "{" ~ WS* ~ code_statements? ~ WS* ~ "}" }
code_block = { "{" ~ WS* ~ code_body ~ WS* ~ "}" }
local_declarations = _{ local_declaration ~ (WS* ~ local_declaration)* }
code_statements = _{ code_statement ~ (WS* ~ code_statement)* }
code_body = { local_declarations? ~ WS* ~ code_statements? }
statement_or_block = _{ code_statement | code_scope }
// TODO: doesn't handle

while_statement = { ^"while" ~ WS* ~ parenthetical_expression ~ WS* ~ statement_or_block }
return_statement = { ^"return" ~ (WS+ ~ expression?) ~ WS* ~ ";" }
constant = _{ literal | unqualified_identifier }
// For some reason unbeknownst to mere mortals, UnrealScript allows two default cases in switch statements.

// CONTROL FLOW
elif_statement = { ^"else" ~ WS+ ~ ^"if" ~ WS* ~ parenthetical_expression ~ WS* ~ statement_or_block }
else_statement = { ^"else" ~ WS* ~ statement_or_block }
if_statement = { ^"if" ~ WS* ~ parenthetical_expression ~ WS* ~ statement_or_block ~ WS* ~ elif_statement* ~ else_statement? }
expression_outer = { expression }
for_statement_inner = { expression_outer? ~ (";" ~ WS* ~ expression_outer?){2} }
for_statement = { ^"for" ~ WS* ~ "(" ~ WS* ~ for_statement_inner ~ WS* ~ ")" ~ WS* ~ statement_or_block }
// foreach_expression = { expression ~ WS+ ~ unqualified_identifier ~ WS* ~ call }
foreach_statement = { ^"foreach" ~ WS+ ~ expression_outer ~ WS* ~statement_or_block }
switch_statement = { ^"switch" ~ WS* ~ parenthetical_expression ~ WS* ~ "{" ~ switch_case* ~ default_case{0,2} ~ "}" }
switch_case_label = { ^"case" ~ WS+ ~ target ~ WS* ~ ":" }
switch_default_label = { ^"default" ~ WS* ~ ":" }
switch_case = { switch_case_label ~ code_statement* }
default_case = { switch_default_label ~ code_statement* }
continue_statement = { ^"continue" ~ WS* ~ ";" }
break_statement = { ^"break" ~ WS* ~ ";" }

// STATES
state_modifier = { ^"auto" }
state_statement = { const_declaration | function_declaration }
state_declaration = { (state_modifier ~ WS+)? ~ ^"state" ~ unqualified_identifier ~ extends? ~ "{" ~ state_ignores? ~ state_statement* ~ state_label* ~ "}" }
state_label = { unqualified_identifier ~ ":" ~ code_statement+ }
state_ignores = { ^"ignores" ~ unqualified_identifier_list ~ ";" }

// STATES
code_statement = { if_statement | for_statement | while_statement | foreach_statement | return_statement | break_statement | continue_statement | compiler_directive | switch_statement | (expression ~ WS* ~ ";") }

// TODO: operator precedence is handled at the parser level
native_verb = {
    "**" |  // 12
    "+=" | "-=" | "*=" | "/=" | // 34
    "&&" | "^^" | // 30
    "*" | "/" /*| ^"dot" | ^"cross"*/ | // 16
    "||" | // 32
    "%" | //18
    "-=" | // 45
    "<<" | ">>" | ">>>" | // 22
    "<=" | ">=" | "==" | "~=" | "<" | ">" /*| ^"clockwisefrom"*/ | // 24
    "!=" | // 26
    "&" | "|" | "^" | // 28
    "$=" | "@=" | //44
    "$" | "@" | // 40
    "+" | "-" | // 20
    "="
}
dyadic_verb = { unqualified_identifier | native_verb }

// USP rules
pass_statement = _{ ^"pass" ~ ";" } // Identical to Python's `pass`, just a statement that does nothing.
